/*
 *  NPCTweak mod by Orions_Stardom and andrewas.
 *  Changes various aspects of BG2 NPCs.
 *  For copyright info, see the file "COPYING" 
 */

BACKUP ~npc_tweak/backup~

AUTHOR ~maguswizardo@gmail.com~

ALWAYS
  // This ALWAYS/ACTION_IF combo will convert charsets for use on EE games.
  ACTION_IF NOT VARIABLE_IS_SET cd_always THEN BEGIN
    OUTER_SET cd_always = 1
    
    ACTION_DEFINE_ARRAY cdnoconvert BEGIN setup END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
    ACTION_DEFINE_ARRAY cdreload BEGIN game END // List of tra files that need to be reloaded after conversion because they were previously loaded in the LANGUAGE section
    
    // On EE games, convert game content tra files to utf-8 so that games don't crash when encountering international characters.
    LAF HANDLE_CHARSETS INT_VAR infer_charset = 1 STR_VAR tra_path = EVAL ~%MOD_FOLDER%/languages~ noconvert_array = cdnoconvert reload_array = cdreload  END
  END
END

VERSION ~v5.1~

README ~npc_tweak/readme-npc_tweak.html~

/* Language Settings */

LANGUAGE
 ~English~
 ~english~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/english/game.tra~

LANGUAGE
 ~Francais (by Graoumf)~
 ~french~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/french/setup.tra~
 ~npc_tweak/languages/english/game.tra~
 ~npc_tweak/languages/french/game.tra~
 
LANGUAGE
 ~German (by Telperion)~
 ~german~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/german/setup.tra~
 ~npc_tweak/languages/english/game.tra~
 ~npc_tweak/languages/german/game.tra~
 
LANGUAGE
 ~Espanol (by Saemon)~
 ~spanish~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/spanish/setup.tra~
 ~npc_tweak/languages/english/game.tra~
 ~npc_tweak/languages/spanish/game.tra~
 
LANGUAGE
 ~Russian (Aerie.ru. Updated by Prowler)~
 ~russian~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/russian/setup.tra~
 ~npc_tweak/languages/english/game.tra~
 ~npc_tweak/languages/russian/game.tra~
 
LANGUAGE
 ~Polish (by Cahir)~
 ~polish~
 ~npc_tweak/languages/english/setup.tra~
 ~npc_tweak/languages/polish/setup.tra~
 ~npc_tweak/languages/english/game.tra~
 ~npc_tweak/languages/polish/game.tra~

/* Anomen */
BEGIN @1000
	/* Bring the new items into existance. */
	COPY ~npc_tweak/Anomen/anohelm.itm~ ~override/anohelm.itm~
		SAY NAME2 @1 // Delryn Family Helmet
		SAY IDENTIFIED_DESC @2 // Passed down through all these people.

	COPY ~npc_tweak/Anomen/anoplat.itm~ ~override/anoplat.itm~
		SAY NAME2 @3 // Anomen's Armour
		SAY IDENTIFIED_DESC @4 // Granted to Ano for being a knight.

	/* Give Anomen his new helmet. */
	COPY_EXISTING_REGEXP ~anom.*cre~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			ADD_CRE_ITEM ~anohelm~ #0 #0 #0 ~IDENTIFIED~ ~helmet~
		END
	BUT_ONLY_IF_IT_CHANGES // It probably will, but this looks nice.

	/* And take it back off him. */
	EXTEND_BOTTOM ~AR1001.bcs~ ~npc_tweak/Anomen/AR1001.baf~
	COPY_EXISTING ~kprunn01.cre~ ~override/g3osmsgr.cre~ // messenger
	              ~amng1.cre~    ~override/g3osamng.cre~ // guard
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			WRITE_EVALUATED_ASCII 0x248 ~%DEST_RES%~ #8 // override script
			WRITE_EVALUATED_ASCII 0x280 ~%DEST_RES%~ #32
			WRITE_EVALUATED_ASCII 0x2cc ~%DEST_RES%~ #8
		END
	BUT_ONLY_IF_IT_CHANGES	

	COMPILE	~npc_tweak/Anomen/AnoStuff.d~
	        ~npc_tweak/Anomen/g3osmsgr.baf~
	        ~npc_tweak/Anomen/g3osamng.baf~
	      
	EXTEND_BOTTOM_REGEXP ~AR0[2-5]00.bcs~ ~npc_tweak/Anomen/messenger_spawn.baf~
	EXTEND_BOTTOM_REGEXP ~AR0[7-9]00.bcs~ ~npc_tweak/Anomen/messenger_spawn.baf~
	EXTEND_BOTTOM        ~AR1000.bcs~     ~npc_tweak/Anomen/messenger_spawn.baf~
	
	/* NPC Kit Pack compatibility, contributed by CamDawg */	
	ACTION_IF FILE_EXISTS_IN_GAME ~ikitano1.spl~ THEN BEGIN
		/* Make platemail usable by Helmites */
		COPY_EXISTING ~anoplat.itm~ ~override~
		 	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
				READ_BYTE  0x29 "use"
				WRITE_BYTE 0x29 ("%use%" BAND 0xFD)
			END
		BUT_ONLY_IF_IT_CHANGES
	END

	ACTION_IF FILE_EXISTS_IN_GAME ~ikitano2.spl~ THEN BEGIN
		/* Make helm usable by beserkers. */
		COPY_EXISTING ~anohelm.itm~ ~override~
 		 PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
 			READ_BYTE  0x2f "use"
			WRITE_BYTE 0x2f ("%use%" BAND 0xFE)		   
 		 END
		BUT_ONLY_IF_IT_CHANGES
	END
	/* End of NPC Kit Pack compatibilty */
	
/* Cernd */
BEGIN @1010
	COPY_EXISTING_REGEXP ~cernd.*cre~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			WRITE_BYTE 0x0244 00
			WRITE_BYTE 0x0245 08
			WRITE_BYTE 0x0246 00
			WRITE_BYTE 0x0247 00
		END
	BUT_ONLY_IF_IT_CHANGES	
	COPY_EXISTING ~npstaf.itm~ ~override/npstaf.itm~
	              ~npclck.itm~ ~override/npclck.itm~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			WRITE_BYTE 0x2b 255
			WRITE_BYTE 0x2d 255
			WRITE_BYTE 0x2f 255
			WRITE_BYTE 0x29 247
 		END
	BUT_ONLY_IF_IT_CHANGES
	
/* Nalia */
BEGIN @1020
	COPY_EXISTING_REGEXP ~nalia.*cre~ ~override~
		PATCH_IF SOURCE_SIZE > 0x2d3 BEGIN
			WRITE_BYTE 0x0045 40
			WRITE_BYTE 0x0067 60
			WRITE_BYTE 0x0069 15
			WRITE_BYTE 0x006a 0
		END
	BUT_ONLY_IF_IT_CHANGES

/* end of file */

	